ext.versionFile = file('version.properties')

task loadVersion {  // task configuration (no 'doLast', 'doFirst')
  // task configurations always executed before task actions
  project.version = readVersion()
}


task printVersion {
  doLast {
    println("Version: ${version}")
  }
}

task makeReleaseVersionNotCustom (
  group: 'versioning', 
  description: 'Make project a release version.') {
  doLast {
    version.release= true
    ant.propertyfile(file: versionFile) {
      entry(key: 'release', type: 'string', operation: '=', value: 'true')
    }
  }
}

task makeReleaseVersion (type: ReleaseVersionTask) {
  // setting custom task properties
  release = version.release
  destFile = versionFile
}

task makeDebugVersion (type: DebugVersionTask) {
  // setting custom task properties
  release = version.release
  destFile = versionFile
}

class ProjectVersion {
  Integer major
  Integer minor
  Boolean release

  ProjectVersion(Integer major, Integer minor) {
    this.major = major
    this.minor = minor
    this.release = Boolean.FALSE
  }

  ProjectVersion(Integer major, Integer minor, Boolean release) {
    this(major, minor)
    this.release = release
  }

  @Override
  String toString() {
    "$major.$minor${release ? '' : '-SNAPSHOT'}"
  }
}

ProjectVersion readVersion() {
  logger.quiet 'Reading the version file.'

  if(!versionFile.exists()) {
    throw new GradleException(
      "Required version file does not exist: $versionFile.canonicalPath")
  }

  Properties versionProps = new Properties()

  versionFile.withInputStream { stream ->
    versionProps.load(stream)
  }
  
  new ProjectVersion (
    versionProps.major.toInteger(),
    versionProps.minor.toInteger(),
    versionProps.release.toBoolean()
  )
}

// creating a custom task class
class ReleaseVersionTask extends DefaultTask {

  // declaring custom task's input output through annotations
  @Input Boolean release
  @OutputFile File destFile 

  // setting task's group and dscription property in consructor
  ReleaseVersionTask() {
    group = 'versioning'
    description = 'Makes project a release version'
  }

  @TaskAction // annotaton declares method to be executed
  void start() {
    project.version.release = true
    ant.propertyfile(file: destFile) {
      entry(key: 'release', type: 'string', operation: '=', value: 'true')
    }
  }
}


class DebugVersionTask extends DefaultTask {

  // declaring custom task's input output through annotations
  @Input Boolean release
  @OutputFile File destFile 

  // setting task's group and dscription property in consructor
  DebugVersionTask() {
    group = 'versioning'
    description = 'Makes project a debug version'
  }

  @TaskAction // annotaton declares method to be executed
  void start() {
    project.version.release = false
    ant.propertyfile(file: destFile) {
      entry(key: 'release', type: 'string', operation: '=', value: 'false')
    }
  }
}
